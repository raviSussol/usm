//%attributes = {"shared":true,"lang":"en"} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): Edwin Monk-Fromont (sussol)
// Date and time: 11/08/15, 11:40:00
// ----------------------------------------------------
// Description:
// Import specific methods, chosen from the file browser
// --[HEADEREND]---------------------------------------
C_LONGINT($index;$error_l)
C_TIME($docRef_h)
C_TEXT($t_document_path)
$t_document_path:=""

C_BOOLEAN(<>VC_Enabled_f)

UTIL_ERR_Startup 
VC_EXPORT_Stop 


If (<>VC_Enabled_f)
C_LONGINT($progID_l;$progThreshold_l)
$progThreshold_l:=1000
$progID_l:=PROG_Create ("Import";4;$progThreshold_l)


PROG_Update ($progID_l;0)

ARRAY TEXT($doclist;0)
$t_document_path:=Select document(Get 4D folder(Database folder;*)+"vc_methods";"TEXT";"Select methods to import";Multiple files;$doclist)

PROG_Update ($progID_l;1/4)

// And now import the code
C_LONGINT($progIDFiles_l)
$progIDFiles_l:=PROG_Create ("Files";Size of array($doclist);$progThreshold_l)

For ($index;1;Size of array($doclist))

// load the code from disk
C_TIME($docRef_h)
UTIL_ERR_HandlerInstall 

C_TEXT($curr_file_path)
$curr_file_path:=$doclist{$index}
$error_l:=UTIL_ERR_HandlerRemove 

UTIL_ERR_HandlerInstall 
$docRef_h:=Open document($curr_file_path;Read mode)
$error_l:=UTIL_ERR_HandlerRemove 

PROG_Update ($progIDFiles_l;$index/Size of array($doclist);$index)

If ($error_l=0)

C_BLOB($methodBody_b;$tempBody_b)
SET BLOB SIZE($methodBody_b;0)
SET BLOB SIZE($tempBody_b;0)
RECEIVE PACKET($docRef_h;$methodBody_b;Get document size($curr_file_path))
CLOSE DOCUMENT($docRef_h)

// remove the BOM
_o_C_INTEGER($begin_offset)
$begin_offset:=0
COPY BLOB($methodBody_b;$tempBody_b;$begin_offset;0;BLOB size($methodBody_b)-$begin_offset)

// convert to text
$methodBody:=Convert to text($tempBody_b;"UTF-8")
// error empty method code
$error_l:=Choose(Length($methodBody)=0;51;0)
If ($error_l=0)
// fix line endings
C_TEXT($methodBody)
$methodBody:=Replace string($methodBody;Char(Line feed);Char(Carriage return))
End if 



End if 

If ($error_l=0)
// system path to method path
C_TEXT($DOAPath_t)
$DOAPath_t:=Replace string($doclist{$index};Get 4D folder(Database folder;*)+"vc_methods:";"")
$DOAPath_t:=Replace string($DOAPath_t;".txt";"")
$DOAPath_t:=Replace string($DOAPath_t;"\\";"/")// Convert \ to / for Windows
$DOAPath_t:=Replace string($DOAPath_t;":";"/")

// load the code
UTIL_ERR_HandlerInstall 
METHOD SET CODE($DOAPath_t;$methodBody;*)
$error_l:=UTIL_ERR_HandlerRemove 
$error_l:=Choose(($error_l=-9766) | ($error_l=-9768);0;$error_l)// Ignore method open errors when doing full import

End if 
If ($error_l#0)
$index:=Size of array($doclist)+1// break of for-loop
End if 

End for 

PROG_Update ($progID_l;1)

Else 
// This should be impossible, because you have to open
// a method in order to run the the macro that calls
// this method...opening a method launches the component.
$error_l:=<>ERR_VC_NotEnabled_l
End if 


If ($error_l#0)
UTIL_ERR_HandleError ($error_l;<>VC_ComponentName_t;UTIL_ERR_ErrorMethod_t;"Error while importing")
Else 
UTIL_Alert ("Forced import successful.")
End if 

PROG_CloseAll 
VC_EXPORT_Start 